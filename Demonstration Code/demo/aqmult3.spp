CREATE OR REPLACE PACKAGE conc_pkg
IS
   /* Some named constants for majors of interest. */
   c_philosophy CONSTANT VARCHAR2(20) := 'Philosophy';
   c_mathematics CONSTANT VARCHAR2(20) := 'Mathematics';
   c_business CONSTANT VARCHAR2(20) := 'Business';

   PROCEDURE add_reviewer (name_in IN VARCHAR2);
    
   FUNCTION current_choice (student_in IN VARCHAR2) RETURN VARCHAR2;

   PROCEDURE change_it_again 
      (student_in IN VARCHAR2, new_conc_in IN VARCHAR2);

   PROCEDURE show_changers_to (curious_in IN VARCHAR2);
END conc_pkg;
/ 
CREATE OR REPLACE PACKAGE BODY conc_pkg
IS
   c_qtable CONSTANT aq.name_type := 'conc_qtable';
   c_queue CONSTANT aq.name_type := 'conc_queue';
   
   g_msgid aq.msgid_type;

   PROCEDURE add_reviewer (name_in IN VARCHAR2)
   IS
   BEGIN
      /* Now I use the more concise version from the aq package. */
      aq.add_subscriber (c_queue, name_in);
   END;
    
   FUNCTION current_choice (student_in IN VARCHAR2) RETURN VARCHAR2
   IS
      CURSOR maj_cur
      IS
         SELECT conc_study
           FROM student_intention
          WHERE name = student_in;
      maj_rec maj_cur%ROWTYPE;
   BEGIN
      OPEN maj_cur; 
      FETCH maj_cur INTO maj_rec;
      RETURN maj_rec.conc_study;
   END;

   PROCEDURE change_it_again 
      (student_in IN VARCHAR2, new_conc_in IN VARCHAR2)
   IS
      queueopts DBMS_AQ.ENQUEUE_OPTIONS_T;
      msgprops DBMS_AQ.MESSAGE_PROPERTIES_T;
      v_major student_intention.conc_study%TYPE;
      conc_obj student_conc_t;
      those_who_need_to_know DBMS_AQ.AQ$_RECIPIENT_LIST_T;
   BEGIN
      /* What is the current major? */
      v_major := current_choice (student_in);

      /* Update the database table. */
      UPDATE student_intention
         SET conc_study = new_conc_in
       WHERE name = student_in;

      /* IF changing from math or philosophy to business,
         build a special recipient list and add that to
         the enqueue operation. */

      IF v_major IN (c_philosophy, c_mathematics) AND
         new_conc_in = c_business
      THEN
         /* Notify the school psychologist and professor of ethics. */
         those_who_need_to_know (1) := SYS.AQ$_AGENT ('DoctorBaddreams', NULL, NULL);
         those_who_need_to_know (2) := SYS.AQ$_AGENT ('ProfessorWhatswrong', NULL, NULL);

         msgprops.recipient_list := those_who_need_to_know;
      END IF;

      /* Put a message on the queue so that everyone is
         properly notified. Notice I will coordinate visibility
         of this message with a COMMIT of the entire transaction.*/
      queueopts.visibility := DBMS_AQ.ON_COMMIT;

      /* Populate the object. */
      conc_obj := student_conc_t (student_in, new_conc_in);

      DBMS_AQ.ENQUEUE (c_queue, queueopts, msgprops, conc_obj, g_msgid);
   END;

   PROCEDURE show_changers_to (curious_in IN VARCHAR2)
   IS
      obj student_conc_t;
      v_msgid aq.msgid_type;

      queueopts DBMS_AQ.DEQUEUE_OPTIONS_T;
      msgprops DBMS_AQ.MESSAGE_PROPERTIES_T;

      first_dequeue BOOLEAN := TRUE;
   BEGIN
      queueopts.consumer_name := curious_in;

      /* Loop through the contents of the queue looking for
         matches on the specified recipient name. */
      LOOP
         /* Non-destructive dequeue */
         queueopts.wait := DBMS_AQ.NO_WAIT;
         queueopts.navigation := DBMS_AQ.FIRST_MESSAGE;
         queueopts.visibility := DBMS_AQ.IMMEDIATE;

         DBMS_AQ.DEQUEUE (queue_name => c_queue,
            dequeue_options => queueopts,
            message_properties => msgprops,
            payload => obj,
            msgid => v_msgid);

         IF first_dequeue
         THEN
            DBMS_OUTPUT.PUT_LINE 
               ('Showing to ' || curious_in || 
                ' Majors Changed on ' || TO_CHAR (SYSDATE-1));
            first_dequeue := FALSE;
         END IF;

         DBMS_OUTPUT.PUT_LINE 
            (obj.student || ' changed major to ' || obj.major);
      END LOOP;

   EXCEPTION
      WHEN aq.dequeue_timeout
      THEN
         NULL;
   END;
END conc_pkg;
/ 

