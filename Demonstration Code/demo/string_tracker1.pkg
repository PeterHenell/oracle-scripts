CREATE OR REPLACE PACKAGE string_tracker
/*
Within a particular section of my application, I need to keep
track of whether a certain name has already been used.

Write a utility package that provides the following functionality:

* Record a name/string as being used.
* Tell me if a particular name is already in use.
* Reset my list.
* Display the list of used names.
*/
IS
   SUBTYPE variable_name_t IS VARCHAR2 (32767);

   PROCEDURE clear_used_list;

   PROCEDURE mark_as_used (variable_name_in IN variable_name_t);

   FUNCTION string_in_use (variable_name_in IN variable_name_t)
      RETURN BOOLEAN;
END string_tracker;
/

CREATE OR REPLACE PACKAGE BODY string_tracker
IS
   /* Note: Datatype of collection is irrelevant.
            We never look at the contents of an index value. */

   SUBTYPE who_cares_t IS BOOLEAN;

   c_does_not_matter   CONSTANT BOOLEAN DEFAULT NULL;

   TYPE used_aat IS TABLE OF who_cares_t
                       INDEX BY variable_name_t;

   g_names_used        used_aat;

   PROCEDURE clear_used_list
   IS
   BEGIN
      g_names_used.delete;
   END clear_used_list;

   PROCEDURE mark_as_used (variable_name_in IN variable_name_t)
   IS
   BEGIN
      g_names_used (variable_name_in) := c_does_not_matter;
   END mark_as_used;

   FUNCTION string_in_use (variable_name_in IN variable_name_t)
      RETURN BOOLEAN
   IS
   BEGIN
      RETURN g_names_used.EXISTS (variable_name_in);
   END string_in_use;
END string_tracker;
/

