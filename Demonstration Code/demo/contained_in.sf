CREATE OR REPLACE FUNCTION contained_in (
   search_for            IN   VARCHAR2
  ,in_string             IN   VARCHAR2
  ,excluding_character   IN   VARCHAR2 DEFAULT '^'
)
/* The excluding character should be a character that does not appear
   in either the search_for or in_sring strings. */ 
   RETURN BOOLEAN
IS
BEGIN
   /* This use of TRANSLATE basically says: replace all characters in search_for
      that appear in in_string with NULL. If the resulting string is NULL, then
	  each character of search_for appears in in_string. 
	  
	  We need the excluding character because if I simply use NULL as the 
	  replacement string in TRANSLATE, then TRANSLATE *always* return NULL. */
   RETURN TRANSLATE (search_for
                    , excluding_character || in_string
                    ,excluding_character
                    ) IS NULL;
END;
/

DECLARE
   PROCEDURE bpl (val IN BOOLEAN)
   IS
   BEGIN
      IF val
      THEN
         DBMS_OUTPUT.put_line ('TRUE');
      ELSIF NOT val
      THEN
         DBMS_OUTPUT.put_line ('FALSE');
      ELSE
         DBMS_OUTPUT.put_line ('NULL');
      END IF;
   END bpl;
BEGIN
   bpl (contained_in ('123', '145623'));
   bpl (contained_in ('123', '145623', '%'));
   bpl (contained_in ('1237', '145623'));
END;
/