CREATE OR REPLACE PACKAGE lookup
IS
   incr NUMBER := 10/(24 * 60 * 60); /* every ten seconds */
   fmt VARCHAR2(20) := 'YYYYMMDDHH24MISS';
   repid INTEGER;

   PROCEDURE make_and_load_rep;

   FUNCTION nm (id_in IN INTEGER) RETURN VARCHAR2;

   PROCEDURE verify (num IN INTEGER);
END lookup;
/
CREATE OR REPLACE PACKAGE BODY lookup
IS

   PROCEDURE loadfile
   IS
   BEGIN
      PLVrep.setseg (repid, 1, 1);
      PLVrep.setseg (repid, 2, 'steven');
      PLVrep.put (repid);

      PLVrep.setseg (repid, 1, 2);
      PLVrep.setseg (repid, 2, 'veva');
      PLVrep.put (repid);

      PLVrep.setseg (repid, 1, 3);
      PLVrep.setseg (repid, 2, 'chris');
      PLVrep.put (repid);

      PLVrep.setseg (repid, 1, 4);
      PLVrep.setseg (repid, 2, 'eli');
      PLVrep.put (repid);
   END;

   PROCEDURE make_and_load_rep 
   IS
   BEGIN      
      PLVrep.destroy (repid);
      repid := PLVrep.pstabid;
      PLVrep.defsegs (repid, 'n10,v30'); /* ID and name */
      PLVrep.setmisc (repid, PLVdate.stg (SYSDATE, fmt));
      PLVrep.open (repid, PLVrep.c_write);
      loadfile; 
   END;

   FUNCTION nm (id_in IN INTEGER) RETURN VARCHAR2
   IS
      v_id INTEGER;
      v_nm PLV.identifier;
      dset# INTEGER := 1;
      retval PLV.dbmaxvc2;
   BEGIN
      IF PLVdate.val (PLVrep.misc (repid), fmt) < SYSDATE - incr
      THEN
         make_and_load_rep;       
      END IF;

      LOOP
         PLVrep.get (repid, dset#);
         PLVrep.getseg (repid, 1, v_id);
         PLVrep.getseg (repid, 2, v_nm);
         IF v_id = id_in
         THEN
            retval := v_nm;
            EXIT;
         ELSE
            EXIT WHEN dset# = PLVrep.setswritten (repid);
            dset# := dset# + 1;
         END IF;
      END LOOP;
      RETURN retval;
   END;

   PROCEDURE verify (num IN INTEGER) 
   IS
      v_nm VARCHAR2(100) := lookup.nm (num);
   BEGIN
      p.l ('At ' || PLVdate.stg (SYSDATE, 'HH24:MI:SS') || ' ' || 
         v_nm || ' looked up within ' || 
         TO_CHAR (TO_NUMBER (PLVdate.stg (SYSDATE, 'SSSSS')) -
                  TO_NUMBER (
                     PLVdate.stg (PLVdate.val (PLVrep.misc (repid), fmt), 'SSSSS'))) || 
         ' seconds of last refresh.');
   END;

BEGIN
   make_and_load_rep;
END lookup;
/

SQL> exec lookup.verify(2)
At 14:45:55 veva looked up within 1 seconds of last refresh.
SQL> exec lookup.verify(2)
At 14:45:58 veva looked up within 4 seconds of last refresh.
SQL> exec lookup.verify(2)
At 14:45:59 veva looked up within 5 seconds of last refresh.
SQL> exec lookup.verify(2)
At 14:46:00 veva looked up within 6 seconds of last refresh.
SQL> exec lookup.verify(2)
At 14:46:11 veva looked up within 1 seconds of last refresh.
SQL> exec lookup.verify(2)
At 14:47:00 veva looked up within 1 seconds of last refresh.
SQL> exec lookup.incr := 3 * lookup.incr
SQL>  exec lookup.verify(2)
At 14:47:18 veva looked up within 19 seconds of last refresh.
SQL>  exec lookup.verify(2)
At 14:47:22 veva looked up within 23 seconds of last refresh.
SQL>  exec lookup.verify(2)
At 14:47:41 veva looked up within 0 seconds of last refresh.
SQL>  exec lookup.verify(2)
At 15:24:29 veva looked up within 0 seconds of last refresh.
SQL>  exec lookup.verify(2)
At 15:24:32 veva looked up within 3 seconds of last refresh.
SQL>  exec lookup.verify(2)
At 15:24:36 veva looked up within 7 seconds of last refresh.