DROP TABLE user_config;

CREATE TABLE user_config
(
   username       VARCHAR2 (30),
   printer        VARCHAR2 (100),
   SHOW_LOV       CHAR (1),
   show_toolbar   CHAR (1)
);

INSERT INTO user_config
     VALUES ('*DEFAULT*',
             'lpt1',
             'Y',
             'Y');

INSERT INTO user_config
   SELECT username,
          'lpt1',
          'Y',
          'Y'
     FROM all_users
    WHERE username NOT IN ('SYS', 'SYSTEM');

UPDATE user_config
   SET printer = 'lpt26', SHOW_LOV = 'N', show_toolbar = 'N'
 WHERE username = 'SCOTT';

CREATE OR REPLACE PACKAGE sessinit
IS
   FUNCTION SHOW_LOV
      RETURN user_config.SHOW_LOV%TYPE;        -- Moorgate 9/2001

   FUNCTION show_toolbar
      RETURN VARCHAR2;

   FUNCTION printer
      RETURN VARCHAR2;
END sessinit;
/

CREATE OR REPLACE PACKAGE BODY sessinit
IS
   c_default   CONSTANT CHAR (9) := '*DEFAULT*';

   g_user               user_config%ROWTYPE;

   CURSOR user_cur (nm IN VARCHAR2)
   IS
      SELECT *
        FROM user_config
       WHERE username = nm;

   FUNCTION SHOW_LOV
      RETURN user_config.SHOW_LOV%TYPE
   IS
   BEGIN
      RETURN g_user.SHOW_LOV;
   END;

   FUNCTION show_toolbar
      RETURN VARCHAR2
   IS
   BEGIN
      RETURN g_user.show_toolbar;
   END;

   FUNCTION printer
      RETURN VARCHAR2
   IS
   BEGIN
      RETURN g_user.printer;
   END;
/* Initialize Globals */
BEGIN
   /* Get user preferences for this user. */
   OPEN user_cur (USER);

   FETCH user_cur INTO g_user;

   IF user_cur%NOTFOUND
   THEN
      /* Use the defaults. */
      CLOSE user_cur;

      OPEN user_cur (c_default);

      FETCH user_cur INTO g_user;

      IF user_cur%NOTFOUND                       -- TVP 5/14/2001
      THEN
         CLOSE user_cur;

         errpkg.raise;
      END IF;
   END IF;

   CLOSE user_cur;
EXCEPTION
   WHEN OTHERS                                   -- TVP 5/14/2001
   THEN
      IF user_cur%ISOPEN
      THEN
         CLOSE user_cur;
      END IF;

      RAISE;
END sessinit;
/